#compdef bundle

# Bundler ZSH completion function
# Drop this somewhere in your $fpath (like /usr/share/zsh/site-functions)
# and rename it _bundle
#
# altered from _brew

_bundled_gems() {
  if [[ -f Gemfile ]] then
    bundled_gems=(`bundle list 2>/dev/null | awk ' NR > 1 { print $2 } '`)
  else
    bundled_gems=()
  fi
}

local -a _1st_arguments
_1st_arguments=(
  'check:Determine whether the requirements for your application are installed and available to bundler'
  'config:Specify and read configuration options for bundler'
  'console:Start an IRB session in the context of the current bundle'
  'exec:Execute a script in the context of the current bundle'
  'gem:Create a simple gem, suitable for development with bundler'
  'help:Describe available tasks or one specific task'
  'init:Generate a simple Gemfile, placed in the current directory'
  'install:Install the gems specified by the Gemfile or Gemfile.lock'
  'list:Show all of the gems in the current bundle'
  'open:Open an installed gem in the editor'
  'package:Package the .gem files required by your application into the vendor/cache directory'
  'show:Show the source location of a particular gem in the bundle'
  'unlock:Unlock the bundle'
  'update:Update dependencies to their latest versions'
  'version:Print version information'
  'viz:Generate a visual representation of your dependencies'
)
local expl
local -a bundled_gems

_arguments \
  '(--no-color)--no-color[Prints all output without color]' \
  '(--version)--version[Version information]' \
  '*:: :->subcmds' && return 0

if (( CURRENT == 1 )); then
  _describe -t commands "bundle subcommand" _1st_arguments
  return
fi

case "$words[1]" in
  show|open)
    _bundled_gems
    _wanted bundled_gems expl 'bundled gems' compadd -a bundled_gems
  ;;
  install)
    _arguments \
      '(--no-color)--no-color[disable colorization in output]' \
      '(--local)--local[do not attempt to connect to rubygems.org]' \
      '(--quiet)--quiet[only output warnings and errors]' \
      '(--gemfile)--gemfile=-[use the specified gemfile instead of Gemfile]:gemfile' \
      '(--system)--system[install to the system location]' \
      '(--deployment)--deployment[install using defaults tuned for deployment environments]' \
      '(--frozen)--frozen[do not allow the Gemfile.lock to be updated after this install]' \
      '(--path)--path=-[specify a different path than the system default]:path:_files' \
      '(--binstubs)--binstubs=-[generate bin stubs for bundled gems to ./bin]:directory:_files' \
      '(--without)--without=-[exclude gems that are part of the specified named group]:groups'
  ;;
  'exec')
    _arguments '*:tail:_normal'
  ;;
esac
