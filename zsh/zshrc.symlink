export DOT=$HOME/.dotfiles

# Only add unique items to path
typeset -U path
path=(
  $DOT/bin
  /usr/local/bin
  /usr/local/sbin
  $path
)

# Only add unique items to fpath
typeset -U fpath
fpath=(
  ${plugins:+${0:h}/plugins/${^plugins}/{functions,completions}(/FN)}
  $DOT/zsh/{functions,completions}(/FN)
  $fpath
)

autoload -U $DOT/zsh/functions/*(:t)

# Load and initialize the completion system ignoring insecure directories.
autoload -Uz compinit && compinit -i

if (( $+commands[mvim] )); then
  export EDITOR="mvim"
else
  export EDITOR="vim"
fi

# Autoload Zsh functions.
autoload -Uz age
autoload -Uz zargs
autoload -Uz zcalc
autoload -Uz zmv
autoload -Uz cdr

# Source plugins defined in ~/.zshrc.
for plugin in $DOT/zsh/plugins/*/init.zsh; do
  source $plugin
done

for config_file ($DOT/**/*.zsh) source $config_file
