export DOT=$HOME/.dotfiles

# Only add unique items to path
typeset -U path
path=(
  $DOT/bin
  /usr/local/bin
  /usr/local/sbin
  $path
)

# Only add unique items to fpath
typeset -U fpath
fpath=(
  ${plugins:+${0:h}/plugins/${^plugins}/{functions,completions}(/FN)}
  $DOT/zsh/{functions,completions}(/FN)
  /usr/local/share/zsh-completions
  $fpath
)

autoload -U $DOT/zsh/functions/*(:t)

# Load and initialize the completion system ignoring insecure directories.
autoload -Uz compinit && compinit -i

# Compile zcompdump, if modified, to increase startup speed.
if [ "$HOME/.zcompdump" -nt "$HOME/.zcompdump.zwc" ]; then
  zcompile "$HOME/.zcompdump"
fi

if (( $+commands[mvim] )); then
  export EDITOR="mvim"
else
  export EDITOR="vim"
fi

# Source plugins defined in ~/.zshrc.
for plugin in $DOT/zsh/plugins/*/init.zsh; do
  # echo $(python -c 'import time; print time.time()') $plugin
  source $plugin
done

for config_file ($DOT/**/*.zsh); do
  # echo $(python -c 'import time; print time.time()') $config_file
  source $config_file
done
